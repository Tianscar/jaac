plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

ext {
    libraryName = 'Java Advanced Audio Decoder'
    libraryGroupName = 'com.tianscar.javasound'
    libraryVendorName = 'Tianscar'

    libraryVersionCode = 5
    libraryVersionName = '0.9.3'

    librarySourceCompatibility = JavaVersion.VERSION_1_8
    libraryTargetCompatibility = JavaVersion.VERSION_1_8
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = librarySourceCompatibility
targetCompatibility = libraryTargetCompatibility

group = libraryGroupName
version = libraryVersionName

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = libraryGroupName
            artifactId = rootProject.name
            version = libraryVersionName
            from components.java
            pom {
                name = libraryName
                description = 'AAC decoder and MP4 demultiplexer library written completely in Java'
                url = 'https://github.com/Tianscar/jaadec'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Tianscar'
                        name = 'Karstian Lee'
                        email = 'tianscar@protonmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com/Tianscar/jaadec.git'
                    developerConnection = 'scm:git:git@github.com/Tianscar/jaadec.git'
                    url = 'https://github.com/Tianscar/jaadec'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options.addStringOption("Xdoclint:none", "-quiet") // TODO cleanup JavaDocs
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}